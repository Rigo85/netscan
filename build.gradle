/**
 * Author Rigoberto Leander Salgado Reyes <rlsalgado2006@gmail.com>
 * <p/>
 * Copyright 2015 by Rigoberto Leander Salgado Reyes.
 * <p/>
 * This program is licensed to you under the terms of version 3 of the
 * GNU Affero General Public License. This program is distributed WITHOUT
 * ANY EXPRESS OR IMPLIED WARRANTY, INCLUDING THOSE OF NON-INFRINGEMENT,
 * MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. Please refer to the
 * AGPL (http:www.gnu.org/licenses/agpl-3.0.txt) for more details.
 */

allprojects {
    apply plugin: 'java'
    group 'org.netscan'
    version '0.1'

    repositories {
        mavenCentral()
    }

}

project(':core') {
    sourceCompatibility = 1.8

    dependencies {
        compile 'jcifs:jcifs:1.3.17'
        compile 'com.google.code.gson:gson:2.4'
        compile 'io.reactivex:rxjava:1.0.15'
    }
}

sourceCompatibility = 1.8

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile project(':core')
}


task run(type: JavaExec) {
    main = "org.netscan.mvc.NetScanApp"
    classpath = sourceSets.main.runtimeClasspath
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'NetScan',
                'Implementation-Version': version,
                'Main-Class': 'org.netscan.mvc.NetScanApp'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }{
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}
